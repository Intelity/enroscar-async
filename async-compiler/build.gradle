apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'

dependencies {
  compile project(':async-interface')
  compile project(':async-rx-annotations')
  compile 'com.squareup:javawriter:2.5.0'

  testCompile 'junit:junit:4.12'
  testCompile 'com.google.testing.compile:compile-testing:0.6'
  testCompile 'com.google.guava:guava:18.0'
  testCompile 'com.google.truth:truth:0.27'

  testRuntime files("${System.properties['java.home']}/../lib/tools.jar")
  testRuntime files("${System.env['ANDROID_HOME']}/platforms/android-19/android.jar")
  testRuntime 'io.reactivex:rxjava:1.0.14'
}

// Get async module classes for tests.
def runtimeProjecs = [':async', ':async-rx']
runtimeProjecs.each {
  evaluationDependsOn(it)
  def classesTask = project(it).tasks.compileReleaseJavaWithJavac
  def classesDir = classesTask.destinationDir
  dependencies {
    testCompile files(classesDir)
  }
  compileTestJava.dependsOn classesTask
}

// Add Android repos.
def logger = new com.android.build.gradle.internal.LoggerWrapper(project.logger)
def sdkHandler = new com.android.build.gradle.internal.SdkHandler(project, logger)
for (File file : sdkHandler.sdkLoader.repositories) {
  project.repositories.maven {
    url = file.toURI()
  }
}

tasks.withType(Test) {
  scanForTestClasses = false
  include "**/*Test.class"
}

modifyPom {
  project {
    name 'Enroscar Async Compiler'
    description 'Processor for @Load/@Send annotations'
  }
}
