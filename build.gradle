buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.2.3'
    classpath 'org.robolectric:robolectric-gradle-plugin:1.0.0'
    classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
  }
}

ext {
  androidCompileSdkVersion = 22
  androidBuildToolsVersion = '22.0.1'
  androidTargetSdkVersion = 22

  testDeps = [
    'org.mockito:mockito-all:1.9.5',
    'org.assertj:assertj-core:1.7.1',
    'com.squareup.assertj:assertj-android:1.0.1'
  ]

  commonPomPart = {
    url 'https://github.com/stanfy/enroscar-async'
    inceptionYear '2011'

    scm {
      url 'https://github.com/stanfy/enroscar-async'
      connection 'scm:git:git@github.com:stanfy/enroscar-async.git'
      developerConnection 'scm:git:git@github.com:stanfy/enroscar-async.git'
    }

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    developers {
      developer {
        id 'rmazur'
        name 'Roman Mazur'
      }
    }
  }
}

subprojects {
  group = "com.stanfy.enroscar"
  version = '2.0-SNAPSHOT'
  repositories {
    mavenCentral()
    mavenLocal()
  }
  plugins.withType(JavaPlugin) {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    archivesBaseName = "enroscar-${project.name}"
  }
  plugins.withType(com.bmuschko.gradle.nexus.NexusPlugin) {
    modifyPom {
      project commonPomPart
    }
  }
}
